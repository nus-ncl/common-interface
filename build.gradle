buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.6.RELEASE'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'org.sonarqube' version '2.0.1'
}
apply plugin: 'spring-boot'

group 'sg.ncl'

version "1.0-${System.getenv('TRAVIS_BUILD_NUMBER') == null ? 'SNAPSHOT' : 'b' + System.getenv('TRAVIS_BUILD_NUMBER')}"

// settings for all projects
allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    group 'sg.ncl'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        jcenter()
    }

    dependencyManagement {
        dependencies {
//            dependencySet(group: 'org.hibernate', version: '5.1.0.Final') {
//                entry 'hibernate-core'
//                entry 'hibernate-entitymanager'
//            }
            dependency 'org.apache.commons:commons-lang3:3.4'
            dependency 'io.jsonwebtoken:jjwt:0.6.0'
            dependency 'org.jmockit:jmockit:1.25'
            dependency 'org.projectlombok:lombok:1.16.10'
        }

        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.6.RELEASE'
        }
    }

    jacocoTestReport {
        dependsOn check
        // HACK Jacoco's requirement that every test set has to have at least one test case for generating a report;
        // workaround removes test sets that does not have any test cases
        executionData = files(executionData.findAll({ it.exists() }))
    }
}

configure(subprojects.findAll({ it.name.startsWith('service-') })) {
    apply plugin: 'spring-boot'
}

dependencies {
    // the only compile dependencies needed here are the services projects;
    // there should be no other compile dependencies
    compile subprojects.findAll({ it.name.startsWith('service-') })
    compile 'org.projectlombok:lombok'
    // the runtime dependencies should be a union of all runtime dependencies needed by all subprojects
    runtime 'mysql:mysql-connector-java'
    runtime 'org.postgresql:postgresql'
    runtime 'com.h2database:h2'

    testCompile 'junit:junit'
}

check {
    dependsOn subprojects.check
}

task copyJunitXml(type: Copy) {
    from(subprojects.test.reports.junitXml.destination) {
        include '*.xml'
    }
    into test.reports.junitXml.destination
}

task testReport(type: TestReport) {
    dependsOn check
    reportOn allprojects.test
    destinationDir test.reports.html.destination
    finalizedBy copyJunitXml
}

jacocoTestReport {
    dependsOn allprojects.check
    executionData = fileTree(project.rootDir.absolutePath).include('**/build/jacoco/*.exec')
    sourceDirectories = files(sourceSets.main.java.srcDirs, subprojects.sourceSets.main.java.srcDirs)
    classDirectories = files(sourceSets.main.output.classesDir, subprojects.sourceSets.main.output.classesDir)
    reports {
        xml {
            enabled true
        }
    }
}

coveralls {
    sourceDirs = allprojects.sourceSets.main.java.srcDirs.flatten()
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarqube.com'
        property 'sonar.projectKey', 'nus-ncl:services-in-one'
        property 'sonar.projectName', 'NCLServices'
        property 'sonar.projectVersion', "${project.version}"
        property 'sonar.login', System.getenv('SONAR_TOKEN')
        if (System.getenv('TRAVIS_BRANCH') == 'master') {
            if (System.getenv('TRAVIS_PULL_REQUEST') == 'false') {
            } else {
                property 'sonar.analysis.mode', 'preview'
                property 'sonar.github.pullRequest', System.getenv('TRAVIS_PULL_REQUEST')
                property 'sonar.github.repository', System.getenv('TRAVIS_REPO_SLUG')
                property 'sonar.github.oauth', System.getenv('GITHUB_TOKEN')
            }
        } else {
            property 'sonar.analysis.mode', 'issues'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '2.14'
    distributionUrl "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
