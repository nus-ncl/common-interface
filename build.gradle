buildscript {
    repositories { jcenter() }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'io.spring.dependency-management' version '0.5.4.RELEASE'
}
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/test-integration/java')
        }
        resources {
            srcDir('src/test-integration/resources')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    jcenter()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('javax.inject:javax.inject:1')

    runtime('mysql:mysql-connector-java')

    testCompile('junit:junit')
    testCompile('org.hamcrest:hamcrest-library')
    testCompile('org.apache.commons:commons-lang3:3.4')

    integrationTestCompile('org.springframework.boot:spring-boot-starter-test')

    integrationTestRuntime('com.h2database:h2')
}

task integrationTest(type: Test) {
    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}
